title: Chapter5
---
<<prevent_manual_save>>
<<wait 2>>
<<move_to_camera Chapter5>>
<<free_port_click>>

Passenger: I hate the stench!
Director: It appears our fare payers aren't too keen on the route we have setup.
Director: They don't want to be sat next to those adorable coos on their journey.
Director: Vessels will turn grey when passengers and cargos conflict with each other.
Director: I have bought a second Vessel, the Argyle to help with the situation.
<<declare $mvArgyleGuid = "">>
<<spawn_vessel mvArgyleGuid Vessel "MV Argyle" SecondVesselSpawn>>
<<set $mvButeGuid = "">>
<<find_vessel mvButeGuid "MV Bute">>
Director: Not sure how I would setup the routes, that is down to you! Be creative.
<<jump WaitForVesselHappinessBegin>>
===

title: WaitForVesselHappinessBegin
---
<<declare $mvArgyleDissatisfied = false>>
<<declare $mvButeDissatisfied = false>>
<<on_cargo_dissatisfied_set {$mvArgyleGuid} mvArgyleDissatisfied>>
<<on_cargo_dissatisfied_set {$mvButeGuid} mvButeDissatisfied>>
<<declare $chapter5StopWatchElapsedS = 0>>
<<declare $chapter5StopWatchGuid = "">>
<<create_game_stop_watch chapter5StopWatchGuid>>
<<link_game_stop_watch_to_display {$chapter5StopWatchGuid} 120>>
<<show_game_stop_watch_display>>
Director: Just make sure no one is upset on our 2 boats for 2 minute straight.
<<jump WaitForVesselHappinessLoop>>
===

title: WaitForVesselHappinessLoop
---
<<wait 1>>
<<sample_game_stop_watch {$chapter5StopWatchGuid} chapter5StopWatchElapsedS>>

<<if $mvArgyleDissatisfied || $mvButeDissatisfied>>

    <<reset_game_stop_watch {$chapter5StopWatchGuid}>>

    <<if $mvArgyleDissatisfied>>
        <<set $mvArgyleDissatisfied = false>>
        <<on_cargo_dissatisfied_set {$mvArgyleGuid} mvArgyleDissatisfied>>
    <<endif>>

    <<if $mvButeDissatisfied>>
        <<set $mvButeDissatisfied = false>>
        <<on_cargo_dissatisfied_set {$mvButeGuid} mvButeDissatisfied>>
    <<endif>>

    <<set_auto_advance_dialogue true>>
    Director: The routes didn't seem to work. Let's restart the timer.
    <<set_auto_advance_dialogue false>>

    <<jump WaitForVesselHappinessLoop>>

<<elseif $chapter5StopWatchElapsedS >= 120>>
    <<jump WaitForVesselHappinessComplete>>

<<else>>
    <<jump WaitForVesselHappinessLoop>>
    
<<endif>>
===

title: WaitForVesselHappinessComplete
---
<<unlink_game_stop_watches_to_display {$chapter5StopWatchGuid}>>
<<hide_game_stop_watch_display>>
<<delete_game_stop_watch {$chapter5StopWatchGuid}>>
<<clear_cargo_dissatisfied_listeners {$mvArgyleGuid}>>
<<clear_cargo_dissatisfied_listeners {$mvButeGuid}>>
Director: Well done, you did it.
===
